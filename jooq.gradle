dependencies {

    jooqRuntime group: "com.h2database", name: "h2", version: "1.4.199"
    jooqRuntime group: "org.glassfish.jaxb", name: "jaxb-runtime", version: "2.3.2"
    jooqRuntime group: "com.sun.activation", name: "javax.activation", version: "1.2.0"

    compile group: "javax.annotation", name: "javax.annotation-api", version: "1.3.2"
}

jooq {
    version = "$jooqVersion"
    edition = "OSS"

    sakila(sourceSets.main) {
        String ddlScripts = parsePath("$projectDir/src/main/sql/sakila-ddl.sql")
        String proceduresScripts = parsePath("$projectDir/src/main/sql/sakila-procedures.sql")

        jdbc {
            driver = "org.h2.Driver"
            url = "jdbc:h2:mem:jooqplayground;INIT=RUNSCRIPT FROM '$ddlScripts'\\;RUNSCRIPT FROM '$proceduresScripts'"
        }
        generator {
            name = "org.jooq.codegen.DefaultGenerator"
            database {
                name = "org.jooq.meta.h2.H2Database"
                includes = ".*"
                excludes = "information_schema.*"
            }
            generate {
                // All options:
                // https://www.jooq.org/xsd/jooq-codegen-3.11.0.xsd
                // https://www.jooq.org/doc/3.11/manual/code-generation/codegen-advanced/

                relations = true
                deprecated = false

                records = true
                fluentSetters = true

                pojos = false
                immutablePojos = true
                pojosEqualsAndHashCode = true

                daos = false
                springAnnotations = false
                jpaAnnotations = false

                javaTimeTypes = false
                validationAnnotations = false
            }
            target {
                packageName = "io.kraluk.playground.jooq.db"
            }
        }
    }
}

static def parsePath(String path) {
    path.replaceAll("\\\\", "/")
}

// project.tasks.getByName("compileJava").dependsOn -= "generateSakilaJooqSchemaSource"